#!/bin/tcsh

# Check version
  if ( $?gdvroot ) then
    if ( ! $?gau ) then
      set gau = 'gdv'
    endif
    if ( ! $?gauroot ) then
      set gauroot = "${gdvroot}"
    endif
  else if ( $?g09root ) then
    if ( ! $?gau ) then
      set gau = 'g09'
    endif
    if ( ! $?gauroot ) then
      set gauroot = "${g09root}"
    endif
  else if ( $?g16root ) then
    if ( ! $?gau ) then
      set gau = 'g16'
    endif
    if ( ! $?gauroot ) then
      set gauroot = "${g16root}"
    endif
  else
    echo "ERROR: Gaussian environment undefined in mkcommand"
    exit 1
  endif

# Check the existence of the Gaussian directories
  if ( ! -d "${gauroot}" ) then
    echo "ERROR: Invalid Gaussian directory ${gauroot} in mkcommand"
    exit 1
  else if ( ! -d "${gauroot}/${gau}" ) then
    echo "ERROR: No ${gau} folder in ${gauroot} in mkcommand"
    exit 1
  endif
  
# Source the login file
  if ( ! -f "${gauroot}/${gau}/bsd/${gau}.login" ) then
    echo "ERROR: Gaussian ${gau}.login file not found in mkcommand"
    exit 1
  endif
  source "${gauroot}/${gau}/bsd/${gau}.login"

# Extract the mk command
  alias mk > "mkgau.tmp"
  exit 0
